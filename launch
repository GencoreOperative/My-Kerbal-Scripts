declare parameter launch_heading is 90, target_altitude is -1.

declare function orbital_height {
	return body:atm:height + 10000.
}.

declare function circularise_angle {
	set angle to -1*SHIP:VERTICALSPEED.
	if angle > 20 {
		set angle to 20.
	}
	if angle < 0 {
		set angle to 0.
	}
	lock steering to heading(launch_heading,angle).
}.

set run_mode to 0.
until run_mode = 10 {

	// Start
	if run_mode = 0 {
		set SHIP:CONTROL:PILOTMAINTHROTTLE to 0.
		lock throttle to 1.0.
		sas off.
		stage.
		set run_mode to 1.
	}

	// Run Mode 1: Orientate for launch heading.
	if run_mode = 1 {
		// Heading incrementer (from 0 to desired launch direction in 1000m)
		set target_heading to 0 + launch_heading.
		set new_heading to (target_heading/1000) * SHIP:ALTITUDE.
		lock steering to heading(new_heading,90).

		if SHIP:ALTITUDE > 1000 {
			set run_mode to 2.
		}
	}

	// Run Mode 2: Angle slowly towards horizon at target altitude.
	if run_mode = 2 {
		// Elevation incrementer (from 0 to desired launch height)
		if target_altitude = -1 {
			print "Orb: " + orbital_height.
			set target_altitude to orbital_height.
		}

		set angle to 90 - (90 * (SHIP:APOAPSIS/target_altitude)).
		lock steering to heading(launch_heading,angle).

		print "Tar: " + target_altitude + " Ang: " + angle + "Apo: " + SHIP:APOAPSIS.
		if SHIP:APOAPSIS > target_altitude {
			set run_mode to 3.
		}
	}

	// Run Mode 3: Wait for Apoapsis, position for circularise
	if run_mode = 3 {
		circularise_angle.
		lock throttle to 0.0.

		print "Eta to APO: " + ETA:APOAPSIS.
		if ETA:APOAPSIS < 1 {
			set run_mode to 4.
		}
	}

	// Run Mode 4: Circularise burn
	if run_mode = 4 {
		circularise_angle.
		lock throttle to 1.0.

		print "AP: " + SHIP:APOAPSIS + " PE: " + SHIP:PERIAPSIS.
		if target_altitude - 1000 < SHIP:PERIAPSIS {
			set run_mode to 6.
		}
	}

	// Run Mode 6: Complete, shutdown engine.
	if run_mode = 6 {
		lock throttle to 0.0.
		sas on.
		set run_mode to 10.
	}

	if run_mode = 10 {
		// Insert actions here
		list parts in all_parts.
		for p in all_parts {
			if (p:tag = "antenna") {
				p:getmodule("ModuleRTAntenna"):DOEVENT("Activate").
			}
		}
	}

	// Repeating Staging Logic
	if run_mode > 0 and MAXTHRUST = 0 {
		stage.
	}
}
